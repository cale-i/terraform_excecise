##################################
# CloudWatch Logs
##################################

resource "aws_cloudwatch_log_group" "for_ecs" {
  name              = "/ecs/${var.container_name}"
  retention_in_days = 180
}

data "aws_iam_policy" "ecs_task_execution_role_policy" {
  arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
}

#  
data "aws_iam_policy_document" "ecs_task_execution" {
  # source_json で既存のポリシーを継承(ここではAmazonECSTaskExecutionRolePolicy)
  source_json = data.aws_iam_policy.ecs_task_execution_role_policy.policy

  statement {
    effect    = "Allow"
    actions   = ["ssm:GetParameters", "kms:Decrypt"] # SSM パラメータストア用
    resources = ["*"]
  }
}

module "ecs_task_execution_role" {
  source     = "./modules/iam_role"
  name       = "ecs-task-execution"
  identifier = "ecs-tasks.amazonaws.com"
  policy     = data.aws_iam_policy_document.ecs_task_execution.json
}

